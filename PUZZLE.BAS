10 REM THIS PROGRAM SOLVES A SIMPLE PUZZLE.
20 REM THE GOAL IS TO PLACE A NUMBER
30 REM  OF PAIRS IN A LINE, SUCH THAT THE
40 REM  NUMBER OF PLACES BETWEEN THE TWO NUMBERS
50 REM  IN A PAIR IS EQUAL TO THE NUMBER.
60 REM ONE OF THE TWO SOLUTIONS FOR N=4
70 REM  IS: 2 3 4 2 1 3 1 4.
80 REM FOR N=7 THERE ARE 52 SOLUTIONS,
90 REM  WHICH ARE FOUND IN APPROX 2 MINUTES.

100 REM THE NUMBER OF PAIRS IS IN THE VARIABLE N.
110 REM THE CURRENT PAIR BEING CONSIDERED
120 REM  IS IN THE VARIABLE I.
130 REM THE ALGORITHM IS A RECURSIVE DEPTH FIRST.

300 REM INITIALIZE THE VARIABLE AND PREPARE FOR RECURSION.
310 LET N=7
320 DIM A(N+N-1)
330 DIM S(N)
340 LET I=1
350 P=TI
360 GOSUB 1100
370 Q=TI
380 PRINT "TIME=";(Q-P)/60;"SECONDS"
390 END

1000 REM THE RECURSIVE LOOP.
1010 REM ON ENTRY, THE ARRAY A() CONTAINS
1020 REM  THE PLACES ALREADY OCCUPIED.
1030 REM ON ENTRY, THE VARIBLE I CONTAINS
1040 REM  THE CURRENT PAIR BEING CONSIDERED.
1050 REM ON ENTRY, THE ARRAY S() CONTAINS
1060 REM  POSITIONS OF EACH PAIR SO FAR.

1100 IF I>N THEN GOTO 2000

1110 REM CLEAR START OF THIS PAIR
1120 J=0

1200 REM LOOP OVER ALL POSSIBLE POSITIONS OF THIS PAIR
1210 IF J+I+1 > N+N-1 THEN RETURN
1220 IF A(J) <> 0 OR A(J+I+1) <> 0 THEN GOTO 1500

1300 REM POPULATE PAIR INTO ARRAY
1310 A(J)=I
1320 A(J+I+1)=I

1330 REM UPDATE STACK WITH CURRENT POSITION
1340 S(I)=J

1350 REM RECURSIVELY PLACE THE NEXT PAIR
1360 I=I+1
1370 GOSUB 1100
1380 I=I-1

1400 REM UNDO STACK
1410 J=S(I)

1420 REM UNDO ARRAY
1430 A(J)=0
1440 A(J+I+1)=0

1500 REM SEARCH REMAINING POSITIONS FOR THIS PAIR
1510 J=J+1
1520 GOTO 1200

2000 REM PRINT OUT A SOLUTION
2010 R=R+1
2020 PRINT "SOLUTION";R,
2030 FOR K=0 TO N+N-1
2040 PRINT A(K);
2050 NEXT K
2060 PRINT
2070 RETURN
